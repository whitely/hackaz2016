(*  This is an EBNF grammar of of mediawiki's wikitext format (there is no standard wikitext format)
    it is essentially ripped from https://www.mediawiki.org/wiki/Markup_spec/EBNF under the terms of the CC-BY-SA license *)

(* general *)
digit         = ("1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"|"0");
URL           = { ASCII_letter } "://" { URL_char };
ASCII_letter  = ("a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
                | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
                | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
                | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z");
URL_char      = (ASCII_letter | digit | "-" | "_" | "." | "~" | "!" | "*" | "'" | "(" | ")" | ";"
                | ":" | "@" | "&" | "=" | "+" | "$" | "," | "/" | "?" | "%" | "#" | "[" | "]");
Unicode_char  = #'\p{Print}'
text          = { Unicode_char };

full_pagename = [ namespace, ":" | ":" ] pagename;
namespace     = Unicode_char, { Unicode_char };
pagename      = Unicode_char, { Unicode_char };


(* links *)
start_link    = "[[";
end_link      = "]]";
internal_link = start_link, full_pagename, ["|", label], end_link, label_extension;
external_link = URL | (start_link, URL, [whitespace_Label], endLink, label_extension);
redirect      = "#REDIRECT", internal_link;
header_link   = "/*", text, "*/";
ISBN_link     = digit, ["-"|" "], 3 * digit, ["-"|" "], 5 * digit,
                [("-"|" "),(digit|"X"|"x")];

(* headers *)
header_end  = [whitespace], line_break;
header6     = line_break, "======", [whitespace], text, [whitespace], "======", header_end;
header5     = line_break, "=====",  [whitespace], text, [whitespace], "=====",  header_end;
header4     = line_break, "====",   [whitespace], text, [whitespace], "====",   header_end;
header3     = line_break, "===",    [whitespace], text, [whitespace], "===",    header_end;
header2     = line_break, "==",     [whitespace], text, [whitespace], "==",     header_end;
header1     = line_break, "=",      [whitespace], text, [whitespace], "=",      header_end;
comment     = "<!--", [Text], "-->";
Commentary  = "<comment", [Text], ">", [Text], "</comment>"; (* This works? *)

(* formatting *)
horizontal_rule  = "----", {"-"};
bold_italic text = "'''''", text, "'''''";
bold_text        = "'''", text, "'''";
italic_text      = "''", text, "''";
code_line        = linebreak, " ", text;
nowiki           = "<nowiki>", text, "</nowiki>";

(* lists *)
unordered_list           = "*", text;
continue_unordered list  = (unordered_list|continue_unordered list|":"|"*"|"#"),
                           linebreak, unordered_list;
ordered_list             = "#", text;
continue_ordered list    = (ordered_list|continue_ordered list|":"|"*"|"#"),
                           linebreak, ordered_list;
definition_list          = [text], ":", text;
continue_definition list = (definition_list|continue_definition list|":"|"*"|"#"),
                           linebreak, definition_list;

(* user signatures *)
user_signature           = "~~~";
user_signature with_date = "~~~~";
current_date             = "~~~~~";


(* includes *)
include  = ( template | tplarg ) ;
template = "{{", title, { "|", part }, "}}" ;
tplarg   = "{{{", title, { "|", part }, "}}}" ;
part     = [ name, "=" ], value ;
title    = balanced_text ;
name     = balanced_text ;
value    = balanced_text ;
balanced_text = text_without consecutive_equal braces, { include, text_without consecutive_equal braces } ;


(* behavior switches *)
place_TOC            = {whitespace|linebreak}, "__TOC__",           {whitespace|linebreak};
force_TOC            = {whitespace|linebreak}, "__FORCETOC__",      {whitespace|linebreak};
disable_TOC          = {whitespace|linebreak}, "__NOTOC__",         {whitespace|linebreak};
disable_section edit = {whitespace|linebreak}, "__NOEDITSECTION__", {whitespace|linebreak};

(* tables *)
table_start       = "{|", {style|whitespace}, linebreak;
table_end         = "|}";
table_caption     = "|+", text, linebreak;
table_header cell = (linebreak, "!", ({style|whitespace}- "|"), text)
                  | (tablecell, ("!!" | "||"), ({style|whitespace}- "|"), text);
table_cell        = (linebreak, "|", ({style|whitespace}- "|"), text)
                  | (table_cell, "||", ({style|whiteSpace}- "|"), text);
table_row         = linebreak, "|-", {"-"}, {style|whitespace}, linebreak;

table_body        = ( table_header cell | table_cell ),
                    { table_row, ( table_header cell | table_cell ) };
table             = table_start, [table_caption], [table_row], table_body, table_end;
